;
;	L-os Angeles INIT
;
INIT:
	LD	SP,(#STKAD)
;
	LD	HL,($0001)
	LD	(BOOT+1),HL
;
	LD	HL,#HOT
	LD	(#USR),HL
	LD	($0001),HL
	LD	HL,($0006)
	LD	(#MEMAX),HL
	LD	(#MEMAX1),HL
	LD	HL,EXBAT2
	LD	(#EXBAT+1),HL
;
	LD	HL,(OSFLG)
	LD	A,H
	CP	$1D
	JP	NZ,INIT1
	LD	A,L
	ADD	A,A
	JP	NZ,INIT1
;				;for X1 LSX-Dodgers
	LD	A,($000B)
	LD	(BASE1+2),A
	LD	(BASE2+2),A
	LD	(BASE3+2),A
	LD	(BASE3X+2),A
	LD	(BASE4+2),A
	LD	(BASE5+2),A
	LD	(BASE6+2),A
	LD	(BASE7+2),A
	LD	(BASE8+2),A
	LD	(BASE9+2),A
;
	LD	HL,XPOKE
	LD	(#POKE+1),HL
	LD	HL,XPEEK
	LD	(#PEEK+1),HL
	LD	HL,XPRINT
	LD	(#PRINT+1),HL
	LD	HL,XLOC
	LD	(#LOC+1),HL
	LD	HL,XCSR
	LD	(#CSR+1),HL
	LD	HL,XSCRN
	LD	(#SCRN+1),HL
	LD	HL,XGETL
	LD	(#GETL+1),HL
	LD	HL,XNL
	LD	(#NL+1),HL
	LD	HL,XGETKY
	LD	(#GETKY+1),HL
	LD	HL,XCOLOR
	LD	(#COLOR+1),HL
	LD	HL,XCINIT
	LD	(#CINIT+1),HL
	LD	HL,XS1FD0
	LD	(#S1FD0+1),HL
	LD	HL,$C9A7
	LD	(XFLGET),HL
	LD	HL,$A281
	LD	(VER+1),HL
BASE6:	LD	A,($97)
	LD	(#MAXLN),A
BASE7:	LD	A,($BF)
	LD	(#WK1FD0),A
	JR	INIT2
INIT1:
	CALL	ZPRINT
	DB	$1B,"x5",0
INIT2:
	CALL	#MPRNT
	DB	$0C,"¥L-os Angeles for "
	DB	"MSX-DOS version ",0
	LD	A,(#LVER)
	ADD	A,"0"
	CALL	#PRINT
	LD	A,"."
	CALL	#PRINT
	LD	A,(#LVER+1)
	CALL	#PRTHX
	CALL	#MPRNT
	DB	$0D
	DB	"¥by Gaku Nakagawa as Larn M.R.(Lovers)",13
	DB	"Ok",$0D,0
;
	CALL	INIDPB
	LD	C,$19
	CALL	SYSTEM
	PUSH	AF
	LD	C,$0D
	CALL	SYSTEM
	POP	AF
	LD	E,A
	PUSH	AF
	LD	C,$0E
	CALL	SYSTEM
	POP	AF
	ADD	A,"A"
	LD	(AUTODV),A
	CALL	#SDVSW
	CALL	#CHGDRV
;
	LD	HL,#HOT
	PUSH	HL
	CALL	#BRKEY
	RET	Z
	LD	HL,$0080
	LD	A,(HL)
	OR	A
	JR	Z,EXEAUTO
	INC	HL
	LD	C,A
	LD	B,0
	LD	DE,AUTOP
	LDIR
EXEAUTO:
	LD	DE,AUTOD
	CALL	#CEXE
	LD	DE,AUTOP1
	JP	#CEXE
;
INIDPB:
	LD	HL,(OSFLG)
	LD	A,H
	CP	$1D
	JR	NZ,INIDPB1
	LD	A,L
	ADD	A,A
	JR	C,INIDPBX
;
INIDPB1:
	LD	A,"A"
INIT3:
	PUSH	AF
	CALL	#DEVADX
	PUSH	IY
	POP	DE
	LD	HL,M2D
	LD	BC,18
	LDIR
;
	LD	(IY+!DEVNO),$87
;
	CALL	INIDPBS
	LD	(IY+!SDIR),0
	LD	(IY+!SDIR+1),0
;
	LD	(IY+!NAME),"D"
	LD	(IY+!NAME+1),"O"
	LD	(IY+!NAME+2),"S"
INIT4:
	POP	AF
	LD	(IY+!NAME+3),A
	SUB	"A"
	LD	(IY+!UNITNO),A
	ADD	A,"A"+1
	CP	"H"+1
	JR	C,INIT3
	RET
;
INIDPBX:
	LD	HL,($000B-1)
	LD	L,$DC
	XOR	A
	CALL	$000F
	PUSH	IX
	POP	HL
	LD	DE,DEVICE
	LD	BC,256
	LDIR
;
	LD	A,"A"
INITX3:
	PUSH	AF
	CALL	#DEVADX
	LD	HL,DRD
	LD	(IY+$02),L
	LD	(IY+$03),H
	LD	HL,DWT
	LD	(IY+$04),L
	LD	(IY+$05),H
	CALL	INIDPBS
	POP	AF
	SUB	"A"
	LD	(IY+!UNITNO),A
	ADD	A,"A"+1
	CP	"H"+1
	JR	C,INITX3
	RET
;
INIDPBS:
	LD	HL,(#MTOFFA)
	LD	(IY+!MOTOFF),L
	LD	(IY+!MOTOFF+1),H
;
	LD	HL,(#GNCLA)
	LD	(IY+!GNCL),L
	LD	(IY+!GNCL+1),H
;
	LD	HL,(#SNCLA)
	LD	(IY+!SNCL),L
	LD	(IY+!SNCL+1),H
	RET
;
AUTOD:	DB	"COMMANDE "
AUTODV:	DB	"A:"
AUTOP:	DB	0
AUTOP1:	DB	0
;
PE:
INITE:
