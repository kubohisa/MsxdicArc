;
;	L-os Angeles CPU
;
;	DISK I/O (CPU)
;
CPU:
;
\VER:
	LD	HL,$2080
	RET
;
\RDFAT:
	CALL	\RDFATS
	RET	C
	LD	HL,(#FATBF)
	LD	A,(HL)
	CP	(IY+!FATID)
	JP	NZ,RDDERR
	OR	A
	RET
;
\RDFATS:
	CALL	#DEVADR
;
	CALL	FATS
;
\RDFATL:
	PUSH	BC
	CALL	#DRDC
	POP	BC
	RET	C
	DJNZ	\RDFATL
	RET
;
\WTTRK:
	LD	A,$F0
	JR	\WTTRK1
;
\DWTW:
	CALL	\DWT		;512/sector
	RET	C
;
\DWT:
	LD	A,$A0
\WTTRK1:
	LD	(DWTPAT+1),A
;
	LD	A,5		;ØÄ×²¶³ÝÄ
	LD	(RETRY),A
;
DWT0:
	PUSH	DE
	PUSH	HL
	LD	(DHLPAT+1),HL
	CALL	SEEK		;ÃÞ¨½¸×²Ä
	POP	HL
	JR	C,ERRD
	DI
DWTPAT:
	LD	A,$A0
	CALL	SECSET
	LD	C,$FB
;
DWT1:
	LD	D,(HL)
DWT2:
	LD	A,B
	IN	A,($F8)
	RRCA
	JR	NC,DWT4
	RRCA
	JR	NC,DWT2
DWT3:
	OUT	(C),D
	INC	HL
	JP	DWT1
;
DWT4:
	DEC	A
	JR	Z,DWT3
	INC	A
	EI
	POP	DE
	INC	DE
	CALL	FDMOFF
	RET	Z
	CALL	DISKC
DWTN:
	JR	NZ,DWT0
	PUSH	DE
ERRD:
	POP	DE
ERRW:
	LD	A,$FF
ERRZ:
	CP	A
	SCF
	JP	FDMOFF
;
\RDID:
	LD	A,$C0
	JR	\RDID1
;
\DRDW:
	CALL	\DRD		;512/sector
	RET	C
;
\DRD:
	LD	A,$80
\RDID1:
	LD	(DRDPAT+1),A
	LD	(DWTPAT+1),A
;
	LD	A,5		;ØÄ×²¶³ÝÄ
	LD	(RETRY),A
;
DRD0:
	PUSH	DE
	PUSH	HL
	LD	(DHLPAT+1),HL
	CALL	SEEK		;ÃÞ¨½¸Ø°ÄÞ
	POP	HL
	JR	C,ERRD
	DI
DRDPAT:
	LD	A,$80
	CALL	SECSET
	LD	C,$FB
DRD1:
	LD	A,B
	IN	A,($F8)
	RRCA
	JR	NC,DRD3
	RRCA
	JR	NC,DRD1
	INI
	INC	B
	JP	DRD1
;
DRD3:
	OR	A
	EI
	POP	DE
	INC	DE
	CALL	FDMOFF
	RET	Z
	CALL	DISKC
DRDN:
	JR	NZ,DRD0
	JR	ERRW
;
DISKC:
	DEC	DE
	BIT	3,A
	JR	NZ,\RNF
DISKD:
	LD	HL,RETRY
	DEC	(HL)
DHLPAT:	LD	HL,0
	LD	A,$FF
	RET
;
\RNF:
	LD	HL,(#DRIVE)
	LD	H,$1F
	LD	A,(HL)
	LD	(HL),$FF
	OR	A
	RET	Z
;
	LD	A,2
	LD	(#SEEKSP),A
	JR	DISKD
;
CPUE:
