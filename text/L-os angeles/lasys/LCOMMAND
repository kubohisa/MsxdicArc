;
;	L-os Angeles COMMAND
;
	ORG	$2100
;
HOT:
	LD	SP,(#STKAD)
	CALL	#CINIT
	LD	A,$00
	CALL	#PRINT
	LD	DE,(#KBFAD)
	CALL	#GETL
	CALL	#CMDANL
	CALL	C,#ERROR
	JR	HOT
;
CMDANL:				;$211B
	LD	A,(DE)
	CP	"*"
	RET	Z
	CP	"#"
	INC	DE
	JR	Z,CMDSOS
	DEC	DE
	CALL	#SPSK
	INC	DE
	LD	A,(DE)
	DEC	DE
	CP	$21
	JP	NC,#CEXE
CMDSOS:
	LD	A,(DE)
	PUSH	AF
	CALL	SS1
	POP	AF
	CALL	#CAP
	JP	#EXCMD
;
EXCMD:
	CP	$20
	JP	Z,#CEXE
	CP	"!"
	JP	Z,BOOT
	CP	"D"
	JP	Z,#CDIR
	CP	"M"
	JP	Z,#MON
	CP	"P"
	JP	Z,C_PATH
	CP	"L"
	JR	Z,C_LOAD
	CP	"N"
	JR	Z,C_NAME
	CP	"K"
	JR	Z,C_KILL
	CP	"S"
	JR	Z,C_SAVE
	CP	"J"
	JR	Z,C_JUMP
	CP	"W"
	JR	Z,C_WITH
	CP	"V"
	JR	Z,C_DVSW
;
	AND	A
	RET
;
C_WITH:
	LD	A,(#WIDTH)
	SUB	41
	JP	#WIDCH
;
C_JUMP:
	CALL	#HLHEX
	RET	C
	POP	BC
	LD	BC,#HOT
	PUSH	BC
	JP	(HL)
;
C_LOAD:
	CALL	#FILE
	RET	C
	PUSH	DE
	CALL	#ROPEN
	POP	DE
	RET	C
	CALL	CCHEX
	JR	C,CLD1
	LD	(#DTADR),HL
CLD1:
	CALL	FPRNTW
	CALL	#RDD
	JR	CLOADX
;
C_NAME:
	CALL	#FILE
	RET	C
	LD	A,(DE)
	CP	$20
	JP	Z,#NAME
	SCF
	RET
;
C_KILL:
	CALL	#FILE
	RET	C
	JP	#KILL
;
C_SAVE:
	LD	A,1
	CALL	#FILE
	RET	C
;
	CALL	CCHEX
	RET	C
	LD	(#DTADR),HL
;
	CALL	CCHEX
	RET	C
	LD	BC,(#DTADR)
	SBC	HL,BC
	INC	HL
	LD	(#SIZE),HL
	LD	L,C		;#DTADR
	LD	H,B
	CALL	CCHEX
	LD	(#EXADR),HL
;
	PUSH	DE
	CALL	#WOPEN
	POP	DE
	RET	C
;
	CALL	CCHEX
	JR	C,CSV1
	LD	(#DTADR),HL
;
CSV1:
	CALL	#WRD
CLOADX:
	RET	C
;
	CALL	#MPRNT
	DB	"Ok",$0D,0
	RET
;
C_DVSW:
	LD	A,(DE)
	JR	CDVSW1
;
C_DIR:
	CALL	#FILE
	RET	C
	LD	A,(FNAME)
	CP	$20
	LD	DE,DIRW
	CALL	Z,#FILED
	JP	#DIRX
;
C_EXE:
	LD	HL,PATHD
CEX1:
	LD	A,(HL)
	INC	HL
	CP	"@"
	RET	C
	CALL	#CHGDRV
	JR	C,CEX1
	PUSH	HL
	PUSH	DE
	CALL	#FILEX
	JR	C,CEXX
	INC	DE
	LD	(EXPAT+1),DE
	LD	A,(FNAME)
	CP	$20
	JR	Z,SDVSWX
	CALL	CEX3
CEXX:
	POP	DE
	POP	HL
	JR	CEX1
;
SDVSWX:
	POP	DE
	POP	DE
	LD	A,(#DSK)
CDVSW1:
	JP	#SDVSW
;
CEX3:
	LD	HL,FNAME+8
	LD	A,(HL)
	CP	$20
	JR	NZ,CEX4
	LD	(HL),"B"	;B??
	INC	HL
	LD	(HL),"?"
	INC	HL
	LD	(HL),"?"
CEX4:
	CALL	#ROPEN
	RET	C
	POP	DE
	POP	DE
	POP	DE
	LD	A,(FNAME+$10)
	CP	$A0
	JP	NZ,#EXBAT
CEX5:
	CALL	#RDD
	RET	C
MSXB3:
	LD	HL,(#EXADR)
EXPAT:	LD	DE,0
	XOR	A
	JP	(HL)
;
MSXBIN:
	LD	HL,(#GCBUF)
	INC	HL
	LD	E,(HL)		;¶²¼ ÊÞÝÁ
	INC	HL
	LD	D,(HL)
	INC	HL
	LD	(#DTADR),DE
	LD	C,(HL)		;¼­³Ø®³ ÊÞÝÁ
	INC	HL
	LD	B,(HL)
	INC	HL
	PUSH	HL
	LD	HL,(#MEMAX)
	SBC	HL,BC
	POP	HL
	RET	C
	LD	C,(HL)		;¼Þ¯º³ ÊÞÝÁ
	INC	HL
	LD	B,(HL)
	INC	HL
	LD	(#EXADR),BC
;
	LD	BC,(#SIZE)
	DEC	BC
	LD	A,B
	CP	4
	JR	NC,MSXB2
	DEC	BC		;1 ¸×½À É ¼®Ø
	DEC	BC
	DEC	BC
	DEC	BC
	DEC	BC
	DEC	BC
	LDIR
	JR	MSXB3
MSXB2:
	LD	BC,$0400-7	;2 ¸×½À ²¼Þ®³
	LDIR
	EX	DE,HL
	LD	B,64
	CALL	RDD1
	JR	MSXB3
;
EXBAT:
	CALL	#FGETC
	RET	C
	CP	$FE
	JR	Z,MSXBIN
;
	CALL	#KYBFC
BAT1:
	OR	A
	RET	Z
	CP	$1A
	RET	Z
	CP	$0A
	CALL	NZ,#KYBFS
	CALL	#FGETC
	JR	NC,BAT1
	RET
;
C_PATH:
	EX	DE,HL
	LD	DE,PATHD
	LD	A,(HL)
	OR	A
	JR	NZ,CPATH0
;
	CALL	#MSX
	JP	#LTNL
;
CPATH0:
	LD	B,PATHX
CPATH1:
	LD	A,(HL)
	OR	A
	JR	Z,CPATH2
	CP	"@"
	JR	NC,CPATH2
	LD	A,"@"
CPATH2:
	LD	(DE),A
	INC	DE
	INC	HL
	DJNZ	CPATH1
	AND	A
	RET
;
CCHEX:
	CALL	CCHECK
	SCF
	RET	NZ
	LD	A,(DE)
	CP	$21
	RET	C
	JP	#HLHEX
;
SPS1:
	INC	DE
SPS2:
	LD	A,(DE)
	CP	$21
	JR	NC,SPS1
	RET
;
SS1:
	INC	DE
SSKIP:
	LD	A,(DE)
	CP	$20
	JR	Z,SS1
	RET
;
PATHX	EQU	10
;
PATHD:	DB	"@"
	DS	PATHX-1
	DB	0
;
CMDE:
